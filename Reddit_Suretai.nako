#枠固定にする事でスクロールバーをなくしつつ、WinAPIから無理やりウインドウサイズの変更をサポート
#こうすることでグリッドのサイズ変更がうまくいかないなでしこの仕様を(見た目上)回避しつつメインウインドウに横スクロールバーが発生するダサい状況を回避
#参考　http://nade.jp-pro.net/bbs/bbs3/cbbs.cgi?mode=al2&number=2820&no=0&KLOG=5
母艦のスタイルは「枠固定」
!GWL_STYLE=-16
!WS_SIZEBOX=$40000
SetWindowLong(母艦のハンドル,GWL_STYLE,GetWindowLong(母艦のハンドル,GWL_STYLE)||WS_SIZEBOX)

HTTPオプションは「UA=」&「Windows:Reddit assist tool for 2ch-users v1.10 (by /u/nuta_unagi)」
母艦の幅は615
母艦の高さは670
ウィンドウ背景色で画面クリア
母艦のタイトルは「Redditスレタイ一覧ツール」
文字書体は「Meiryo UI」
文字サイズは9
板リストに「板一覧.txt」を開く

APIタイマーとはタイマー
その間隔は2
その時満ちた時は
	ボタンオン処理
	
板タブとはタブページ
板タブについて
    X=2
    Y=2
そのタブ多段化はオン

HOTボタンとはボタン
HOTボタンについて
    X=2
    Y=955
    W=95
    H=36
    文字サイズは、20。
    テキストは、「注目」。

NEWボタンとはボタン
NEWボタンについて
    X=100
    Y=955
    W=95
    H=36
    文字サイズは、20。
    テキストは、「新着」。

RISINGボタンとはボタン
RISINGボタンについて
    X=198
    Y=955
    W=95
    H=36
    文字サイズは、20。
    テキストは、「上昇度」。

CONTROVERSIALボタンとはボタン
CONTROVERSIALボタンについて
    X=296
    Y=955
    W=95
    H=36
    文字サイズは、20。
    テキストは、「論争中」。

TOPボタンとはボタン
TOPボタンについて
    X=394
    Y=955
    W=95
    H=35
    文字サイズは、20。
    テキストは、「トップ」。
    
検索ボタンとはボタン
検索ボタンについて
    X=500
    Y=955
    W=95
    H=35
    文字サイズは、20。
    テキストは、「検索」。

######################動的生成祭り######################
板リストを反復
	「板タブに『{対象}』をタブ追加」をナデシコする
	「{対象}スレタイ一覧とはグリッド。{対象}スレタイ一覧について
    X=0
    Y=0
    レイアウトは、『全体』。
    アイテムは、『番号,レス,↑,フレア,スレタイ　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　』。
    背景色は、白色。
	」をナデシコする
	板タブの表示タブは(回数-1)
	「{対象}スレタイ一覧の親部品は、板タブ」をナデシコする
	「{板タブのテキスト}URLLIST＝『』」をナデシコする
	板数＝回数
	「{対象}スレタイ一覧をダブルクリックした時は
		URL処理」をナデシコする

######################動的生成祭り######################

板タブに「検索」をタブ追加
板タブの表示タブは板数
「{板タブのテキスト}URLLIST＝『』」をナデシコする
検索スレタイ一覧とはグリッド。検索スレタイ一覧について
    X=0
    Y=0
    W=832
    H=924
    レイアウトは、「全体」。
    アイテムは、『番号,レス,↑,フレア,スレタイ　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　』。
    背景色は、白色。
検索スレタイ一覧の親部品は、板タブ

板タブの表示タブは0

母艦のサイズ変更した時は〜
	CONTROVERSIALボタンのYは((母艦の高さ-32)-（660-595))
	TOPボタンのYは((母艦の高さ-32)-（660-595))
	HOTボタンのYは((母艦の高さ-32)-（660-595))
	NEWボタンのYは((母艦の高さ-32)-（660-595))
	RISINGボタンのYは((母艦の高さ-32)-（660-595))
	検索ボタンのYは((母艦の高さ-32)-（660-595))
	板タブの高さは((母艦の高さ-32)-（660-592))
	#板タブを無理やりウインドウからはみ出させる事でグリッドの不具合を無理やり回避。冒頭のテクとの合わせ技
	#クジラ飛行机さん早くグリッドのスタイル調整まともに出来るようにして・・・
	板タブの幅は(母艦の幅+1000)
	「{板タブのテキスト}スレタイ一覧のセルサイズ自動調節」をナデシコする

板タブを変更した時は
	もし、板タブのテキストが「検索」ならば、
		ボタンオフ処理
		APIタイマーを停止
		0秒待つ
	違えば
		ボタンオン処理
		0秒待つ
HOTボタンをクリックした時は
	APIタイマーを開始
	ボタンオフ処理
	0秒待つ
	JSON＝「http://www.reddit.com/r/{板タブのテキスト}/hot/.json?limit=100」をHTTPデータ取得
	hotraw＝JSONをJSONデコード
	「{板タブのテキスト}URLLIST＝hotrawを板タブのテキストでリスト処理」をナデシコする
NEWボタンをクリックした時は
	APIタイマーを開始
	ボタンオフ処理
	0秒待つ
	JSON＝「http://www.reddit.com/r/{板タブのテキスト}/new/.json?limit=100」をHTTPデータ取得
	newraw＝JSONをJSONデコード
	「{板タブのテキスト}URLLIST＝newrawを板タブのテキストでリスト処理」をナデシコする
RISINGボタンをクリックした時は
	APIタイマーを開始
	ボタンオフ処理
	0秒待つ
	JSON＝「http://www.reddit.com/r/{板タブのテキスト}/rising/.json?limit=100」をHTTPデータ取得
	riseraw＝JSONをJSONデコード
	「{板タブのテキスト}URLLIST＝riserawを板タブのテキストでリスト処理」をナデシコする
CONTROVERSIALボタンをクリックした時は
	APIタイマーを開始
	ボタンオフ処理
	0秒待つ
	JSON＝「http://www.reddit.com/r/{板タブのテキスト}/controversial/.json?limit=100」をHTTPデータ取得
	contraw＝JSONをJSONデコード
	「{板タブのテキスト}URLLIST＝contrawを板タブのテキストでリスト処理」をナデシコする
TOPボタンをクリックした時は
	APIタイマーを開始
	ボタンオフ処理
	0秒待つ
	JSON＝「http://www.reddit.com/r/{板タブのテキスト}/top/.json?limit=100」をHTTPデータ取得
	topraw=JSONをJSONデコード
	「{板タブのテキスト}URLLIST＝toprawを板タブのテキストでリスト処理」をナデシコする

検索ボタンをクリックした時は
	ボタンオフ処理
	APIタイマーを停止
	検索用フォームの可視はオン

検索スレタイ一覧をダブルクリックした時は
	検索URL処理
	
################検索用フォーム#####################
検索用フォームとはフォーム
その幅は346
その高さは170
その可視はオフ
検索の背景色はウィンドウ背景色
文字書体は「Meiryo UI」
文字サイズは9
検索用フォームのタイトルは「検索」

検索エディタとはエディタ
検索エディタについて
    X=8
    Y=29
    W=236
    H=20
    親部品は検索用フォーム
    文字サイズは、8。
    テキストは、「」。
    背景色は、白色。

検索開始ボタンとはボタン
検索開始ボタンについて
    X=254
    Y=26
    W=74
    H=26
    親部品は検索用フォーム
    文字サイズは、15。
    テキストは、「検索」。

検索ラジオとはラジオ
検索ラジオについて
    X=8
    Y=59
    W=320
    H=65
    親部品は検索用フォーム
    テキストは「検索先選択」
    アイテムは、『スレッド一覧
reddit本体
スレタイ検索(サーバー構築予定)
google(気づいたら検索API有料化してた・・・)
』。
	値は0
	有効はオフ

検索ラベルとはラベル
検索ラベルについて
    X=14
    Y=5
    W=117
    H=11
    親部品は検索用フォーム
    文字サイズは、8。
    文字色は、0。
    テキストは、「検索ワードを入れてください」。
    背景色は、15790320。

検索用フォームのサイズ変更した時は〜

検索エディタをクリックした時は
	
検索開始ボタンをクリックした時は
	もし、板タブのテキストが「検索」ならば、
		「絞込み検索は現在サポートされていません」と言う。
		抜ける
	もし、検索ラジオの値が0ならば、
		検索URLLIST＝検索エディタのテキストを板タブのテキストでスレタイ一覧検索処理
	板タブの表示タブは板数
	ボタンオフ処理
	検索用フォームの可視はオフ
	ボタンオフ処理
	
検索ラジオをクリックした時は
	
検索ラベルをクリックした時は
################検索用フォームここまで#####################	

#####各種ユーザー変数
●SetWindowLong(h,i,l) =DLL("user32.dll","long SetWindowLongA(hwnd hWnd,int nIndex,long dwNewLong)")
●GetWindowLong(h,i) = DLL("user32.dll","LONG GetWindowLongA(HWND hWnd,int nIndex)")

●ボタンオン処理
	HOTボタンの有効はオン
	NEWボタンの有効はオン
	RISINGボタンの有効はオン
	CONTROVERSIALボタンの有効はオン
	TOPボタンの有効はオン
	検索ボタンの有効はオン

●ボタンオフ処理
	HOTボタンの有効はオフ
	NEWボタンの有効はオフ
	RISINGボタンの有効はオフ
	CONTROVERSIALボタンの有効はオフ
	TOPボタンの有効はオフ
	検索ボタンの有効はオフ

●URL処理
	「ct＝『{板タブのテキスト}スレタイ一覧の行』」をナデシコする
	CLICK＝「」
	「CLICK＝{ct}-1」をナデシコする
	UL＝「」
	「UL＝{板タブのテキスト}URLLIST￥{CLICK}」をナデシコする
	もしULが空なら、抜ける
	「http://www.reddit.com{UL}」を起動する。
	
●検索URL処理
	「ct＝『検索スレタイ一覧の行』」をナデシコする
	CLICK＝「」
	「CLICK＝{ct}-1」をナデシコする
	UL＝「」
	「UL＝{板タブのテキスト}URLLIST￥{CLICK}」をナデシコする
	もしULが空なら、抜ける
	「http://www.reddit.com{UL}」を起動する。

●リスト処理（対象文字列を,ITAで）
	tmp＝「」
	tmpスレタイ＝「」
	tmpスレタイ【0,0】＝『番号,レス,↑,フレア,スレタイ　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　』
	パターン＝「\Anum_comments=」
	対象文字列を反復
		対象をパターンで正規表現マッチ
		もし、それが空でなければ、
			対象のパターンを「」へ正規表現置き換え
			それをtmpに一行追加
	tmpを改行で区切ってtmpに代入
	tmpを反復
		tmpスレタイ【回数,0】＝回数
		tmpスレタイ【回数,1】＝対象
	tmp＝「」
	パターン＝「\Ascore=」
	対象文字列を反復
		対象をパターンで正規表現マッチ
		もし、それが空でなければ、
			対象のパターンを「」へ正規表現置き換え
			それをtmpに一行追加
	tmpを改行で区切ってtmpに代入
	tmpを反復
		tmpスレタイ【回数,2】＝対象
	tmp＝「」	
	パターン＝「\Alink_flair_text=」
	対象文字列を反復
		対象をパターンで正規表現マッチ
		もし、それが空でなければ、
			対象のパターンを「」へ正規表現置き換え
			それをtmpに一行追加
	tmpを改行で区切ってtmpに代入
	tmpを反復
		tmpスレタイ【回数,3】＝対象
	tmp＝「」	
	パターン＝「\Adata=title=」
	対象文字列を反復
		対象をパターンで正規表現マッチ
		もし、それが空でなければ、
			対象のパターンを「」へ正規表現置き換え
			それをtmpに一行追加
	tmpを改行で区切ってtmpに代入
	tmpを反復
		tmpスレタイ【回数,4】＝対象
	tmp＝「」
	ulist＝「」
	パターン＝「\Apermalink=」
	対象文字列を反復
		対象をパターンで正規表現マッチ
		もし、それが空でなければ、
			対象のパターンを「」へ正規表現置き換え
			それをulistに一行追加
	ulistを改行で区切ってulistに代入

	tmpスレタイ【0,0】＝『番号,レス,↑,フレア,スレタイ』
	「{ITA}スレタイ一覧のアイテムはtmpスレタイ」をナデシコする
//	「{ITA}スレタイ一覧のセルサイズ自動調節」をナデシコする
	ulistで戻る

●スレタイ一覧検索処理(対象文字列を,ITAで)
	tmp＝「」
	tmpスレタイ＝『番号,レス,↑,フレア,スレタイ　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　』
	resultスレタイ＝『番号,レス,↑,フレア,スレタイ　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　
』
	「tmpurl＝{ITA}URLLIST」をナデシコする
	ulist＝「」
	「tmpスレタイ＝{ITA}スレタイ一覧のアイテム」をナデシコする
	tmpスレタイを反復
		もし回数が1ならば
			続ける
		違えば、
			対象で対象文字列を文字検索
			もし、それが0でなければ、
				対象をresultスレタイに一行追加
				tmpurl￥(回数-2)をulistに一行追加
	検索スレタイ一覧のアイテムはresultスレタイ
//	検索スレタイ一覧のセルサイズ自動調節
	ulistで戻る